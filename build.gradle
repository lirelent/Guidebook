buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

repositories {
    maven {
        url 'http://dogforce-games.com/maven'
    }

    maven {
        url 'https://maven.mcmoddev.com'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

version = "2.9.1"
group = "gigaherz.guidebook" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Guidebook-1.12.2"

minecraft {
    version = "1.12.2-14.23.5.2814"
    runDir = "run"
    mappings = "stable_39"

    replace "@VERSION@", project.version
    replaceIn "GuidebookMod.java"
}

dependencies {
    deobfCompile "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"

    deobfCompile "net.darkhax.gamestages:GameStages-1.12.2:2.0.91"
    deobfCompile "net.darkhax.bookshelf:Bookshelf-1.12.2:2.3.552"
}

shadowJar {
    exclude "net/**/*", "gigaherz/common/**/*"
    relocate 'gigaherz.guidebook', 'gigaherz.lirelent.guidebook'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
        
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java

            artifact sourceJar
        }
    }

    repositories {
        maven {
            url "$projectDir/../CommonMaven"
        }
    }
}

